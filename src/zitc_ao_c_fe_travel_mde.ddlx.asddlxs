@Metadata.layer: #CUSTOMER
@UI: {
    headerInfo: {
        typeName: 'Travel',
        typeNamePlural: 'Travels',
        title: {
            type: #STANDARD, value: 'Description'
        },
        description: {
            value: 'TravelID'
        }
    },
    presentationVariant: [{
        sortOrder: [{
            by: 'TravelID',
            direction: #ASC
        },{
            by: 'LocalLastChangedAt',
            direction: #DESC
        }],
        visualizations: [{
            type: #AS_LINEITEM
        }]
    }]
}
annotate view ZITC_AO_C_FE_TRAVEL
    with 
{
    @UI.facet: [
      {
          id: 'TravelHeaderPrice',
          purpose: #HEADER,
          type: #DATAPOINT_REFERENCE,
          position: 10,
          targetQualifier: 'PriceData'
      },
      {
          id: 'TravelHeaderOverallStatus',
          purpose: #HEADER,
          type: #DATAPOINT_REFERENCE,
          position: 20,
          targetQualifier: 'StatusData'
       },
       {
          label: 'General Information',
          id: 'spiderman',
          type: #COLLECTION,
          position: 10
        },
        {
          label: 'General',
          id: 'Travel',
          type: #IDENTIFICATION_REFERENCE,
          purpose: #STANDARD,
          parentId: 'spiderman',
          position: 10
        },
        {
          id: 'Dates',
          purpose: #STANDARD,
          type: #FIELDGROUP_REFERENCE,
          parentId: 'spiderman',
          label: 'Dates',
          position: 30,
          targetQualifier: 'DatesGroup'
        },
        {
          id: 'Prices',
          purpose: #STANDARD,
          type: #FIELDGROUP_REFERENCE,
          parentId: 'spiderman',
          label: 'Prices',
          position: 20,
          targetQualifier: 'PricesGroup'
        },
        {
          id: 'Booking',
          purpose: #STANDARD,
          type: #LINEITEM_REFERENCE,
          label: 'Bookings',
          position: 20,
          targetElement: '_Booking'
        }
      ]

    @UI.selectionField: [{ position: 10 }]
    @UI.identification: [{ position: 10 }]
    @UI.lineItem: [{ position: 10 },
    { type: #FOR_ACTION, label: 'copy Travel', dataAction: 'createTravelByTemplate' }]
    TravelID;
    
    @UI.identification: [{ position: 11 }]
    Description;
    
    @UI.lineItem: [{ position: 20 }]
    @UI.selectionField: [{ position: 10 }]
    @UI.identification: [{ position: 20 }]
    AgencyID;
    
    @UI.lineItem: [{ position: 30 }]
    @UI.selectionField: [{ position: 20 }]
    @UI.identification: [{ position: 30 }]
    CustomerID;
    
    @UI.lineItem: [{ position: 40 }]
    @UI.fieldGroup: [{ qualifier: 'DatesGroup', position: 10 }]
    BeginDate;
    
    @UI.lineItem: [{ position: 50 }]
    @UI.fieldGroup: [{ qualifier: 'DatesGroup', position: 20 }]
    EndDate;
    
    @UI.lineItem: [{ position: 60 }]
    @UI.fieldGroup: [ { qualifier: 'PricesGroup', position: 10} ]
    BookingFee;
    
    @UI.lineItem: [{ position: 70 }]
    @UI.dataPoint: { qualifier: 'PriceData', title: 'Total Price'}
    @UI.fieldGroup: [ { qualifier: 'PricesGroup', position: 20} ]
    TotalPrice;
    
    @UI.lineItem: [{ position: 80, importance: #HIGH, criticality: 'OverallStatusCriticality' }]
    @UI.selectionField: [{ position: 30 }]
    @UI.dataPoint: { qualifier: 'StatusData', title: 'Status', criticality: 'OverallStatusCriticality' }
    
    OverallStatus;
    
       
    @UI.lineItem: [{ position: 90 }]
    LocalLastChangedAt;
    
}